plugins {
    id 'java'
    id 'application'
}

group = 'org.opensearch.knn'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.lucene:lucene-core:10.3.2'
    implementation 'org.apache.lucene:lucene-misc:10.3.2'
    implementation 'org.apache.lucene:lucene-codecs:10.3.2'
    implementation 'org.opensearch:opensearch:3.4.0'
    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation fileTree(dir: 'libs', include: '*.jar')
}

application {
    mainClass = 'org.opensearch.knn.reorder.VectorReorder'
    applicationDefaultJvmArgs = ["-Djava.library.path=${projectDir}/jni/release"]
}

// K-means reorder - usage: ./gradlew kmeansReorder -Pvec=file1.vec,file2.vec [-Pfaiss=file1.faiss,file2.faiss] [-Pspace=l2] [-PefSearch=100] [-PefConstruction=100] [-Pm=16]
tasks.register('kmeansReorder', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.opensearch.knn.reorder.VectorReorder'
    jvmArgs = ["-Djava.library.path=${projectDir}/jni/release", "-Xmx8g"]
    doFirst {
        def argList = ['kmeans-reorder']
        def vecFiles = (project.findProperty('vec') ?: '').split(',').findAll { it }
        vecFiles.each { argList += ['--vec', it] }
        def faissFiles = (project.findProperty('faiss') ?: '').split(',').findAll { it }
        faissFiles.each { argList += ['--faiss', it] }
        if (project.hasProperty('space')) argList += ['--space', project.space]
        if (project.hasProperty('efSearch')) argList += ['--ef-search', project.efSearch]
        if (project.hasProperty('efConstruction')) argList += ['--ef-construction', project.efConstruction]
        if (project.hasProperty('m')) argList += ['--m', project.m]
        args = argList
    }
}

// BP reorder - usage: ./gradlew bpReorder -Pvec=file1.vec,file2.vec [-Pfaiss=file1.faiss,file2.faiss] [-Pspace=l2] [-PefSearch=100] [-PefConstruction=100] [-Pm=16]
tasks.register('bpReorder', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.opensearch.knn.reorder.BpReorderTool'
    jvmArgs = ["-Djava.library.path=${projectDir}/jni/release", "-Xmx8g"]
    doFirst {
        def argList = ['bp-reorder']
        def vecFiles = (project.findProperty('vec') ?: '').split(',').findAll { it }
        vecFiles.each { argList += ['--vec', it] }
        def faissFiles = (project.findProperty('faiss') ?: '').split(',').findAll { it }
        faissFiles.each { argList += ['--faiss', it] }
        if (project.hasProperty('space')) argList += ['--space', project.space]
        if (project.hasProperty('efSearch')) argList += ['--ef-search', project.efSearch]
        if (project.hasProperty('efConstruction')) argList += ['--ef-construction', project.efConstruction]
        if (project.hasProperty('m')) argList += ['--m', project.m]
        args = argList
    }
}

// Test tasks
tasks.register('testKmeansReorder', JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'org.opensearch.knn.reorder.VectorReorderTest'
    jvmArgs = ["-Djava.library.path=${projectDir}/jni/release"]
    workingDir = projectDir
}

tasks.register('testBpReorder', JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'org.opensearch.knn.reorder.BpReordererTest'
    jvmArgs = ["-Djava.library.path=${projectDir}/jni/release", "-Xmx8g"]
    workingDir = projectDir
}

tasks.register('runKMeansTest', JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'org.opensearch.knn.reorder.KMeansTest'
    jvmArgs = ["-Djava.library.path=${projectDir}/jni/release"]
}

tasks.register('runClusterSortTest', JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'org.opensearch.knn.reorder.ClusterSortTest'
    jvmArgs = ["-Djava.library.path=${projectDir}/jni/release"]
}
